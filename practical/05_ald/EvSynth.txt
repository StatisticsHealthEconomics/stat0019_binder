model {
  # 1. Evidence synthesis of relative effectiveness vs placebo
  for (s in 1:S) {
    # Placebo arm
    r0[s] ~ dbin(pi0[s],n0[s])
    logit(pi0[s]) <- alpha[s]
    # NIs arm
    r1[s] ~ dbin(pi1[s],n1[s])
    logit(pi1[s]) <- alpha[s] + delta[s]
    # Study-specific baseline probability of influenza (logit scale)
    alpha[s] ~ dnorm(0,0.1)
    # Study-specific log-OR for NIs 
    delta[s] ~ dnorm(mu.delta,tau.delta)
  }
  # Pooled log-OR for NIs vs placebo
  mu.delta ~ dnorm(0,0.1)
  # Prior on the sd of delta[s]
  sigma.delta ~ dexp(1.65);  tau.delta <- pow(sigma.delta,-2)
  # Pooled log-OR for NI vs placebo
  or <- exp(mu.delta)
  
  # 2. Evidence synthesis for incidence of influenza 
  for (h in 1:H) {
    y[h] ~ dbin(theta[h],m[h])
    logit(theta[h]) <- beta[h]
    beta[h] ~ dnorm(mu.beta,tau.beta)
  }
  # Pooled influenza incidence among adults (with no NIs) on the logit scale
  mu.beta ~ dnorm(0,0.1)
  # Prior on the sd of beta[h]
  sigma.beta ~ dexp(1.65);   tau.beta <- pow(sigma.beta,-2)
  
  # 3. Probability of influenza with and without NIs
  # Rescaled pooled probability of influenza without NIs
  logit(p0) <- mu.beta
  # Rescaled probability of influenza with NIs
  logit(p1) <- logit(p0) + mu.delta
  
  # 4. Outcomes
  # Model for the costs of treating influenza (prec = 1/2.34 = 0.427)
  c.inf ~ dnorm(16.78,0.427)
  # Duration of infection (prec = 1/0.2403 = 4.159)
  l ~ dlnorm(2.075,4.159)
}
