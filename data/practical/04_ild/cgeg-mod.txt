model{
	for(i in 1:N1){
		eff1[i] ~ dgamma(shape.e[1],rate.e1[i]);	phi.e1[i] <- mu.e[1]
		cost1[i] ~ dgamma(shape.c[1],rate.c1[i]);	phi.c1[i] <- mu.c[1]+beta[1]*(eff1[i]-mu.e[1])
		rate.e1[i] <- shape.e[1]/phi.e1[i]
		rate.c1[i] <- shape.c[1]/phi.c1[i]
	}
	for(i in 1:N2){
		eff2[i] ~ dgamma(shape.e[2],rate.e2[i]);	phi.e2[i] <- mu.e[2]
		cost2[i] ~ dgamma(shape.c[2],rate.c2[i]);	phi.c2[i] <- mu.c[2]+beta[2]*(eff2[i]-mu.e[2])
		rate.e2[i] <- shape.e[2]/phi.e2[i]
		rate.c2[i] <- shape.c[2]/phi.c2[i]
	}
# Prior distributions
	for(t in 1:2){
		shape.e[t] ~ dunif(0,10)
		shape.c[t] ~ dunif(0,10)
		mu.e[t] ~ dunif(0, 1000)
		mu.c[t] ~ dunif(0, 1000)
		beta[t] ~ dunif(-5,5)
	}

# HE outcomes
	delta.c <- mu.c[2]-mu.c[1]
	delta.e <- -(mu.e[2] - mu.e[1]) # as days in hospital is a bad thing

#   CEAC curves
	K.space<-0.1  # space between willlingness-to-pay tick marks (100 pounds)
	for(j in 1:11){
	    K[j]<- (j-1)*K.space
	    INB[j] <- K[j]*delta.e - delta.c
	    CEAC[j] <- step( INB[j] )   
	}
}
